apply plugin: 'com.android.application'

android {
    namespace 'com.rdapps.gamepad'
    compileSdk 33
    def VERBOSE = "2"
    def DEBUG = "3"
    def INFO = "4"
    def WARN = "5"
    def ERROR = "6"
    def ASSERT = "7"
    def NONE = "0";

    defaultConfig {
        applicationId "com.rdapps.gamepad"
        minSdkVersion 28
        targetSdkVersion 33
        versionCode 122
        versionName "1.0.91.2"
        resValue "string", "version_name", versionName
        buildConfigField  'boolean', 'CHECK_UPDATE', 'false'
        buildConfigField  'int', 'LOG_LEVEL', VERBOSE
        buildConfigField  'byte[]', 'DEBUG_INPUT', '{0x21, 0x31, 0x30}'
        buildConfigField  'byte[]', 'DEBUG_OUTPUT', '{0x11, 0x01}'
        buildConfigField  'boolean', 'VM', 'false'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'VM', 'true'
        }
        apk {
            buildConfigField 'int', 'LOG_LEVEL', NONE
            buildConfigField 'boolean', 'CHECK_UPDATE', 'true'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField 'int', 'LOG_LEVEL', NONE
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        vm {
            minifyEnabled true
            buildConfigField 'int', 'LOG_LEVEL', NONE
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'boolean', 'VM', 'true'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    lint {
        abortOnError false
    }
    androidResources {
        noCompress 'jcui'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.media:media:1.5.0'
    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.drawerlayout:drawerlayout:1.2.0'
    implementation 'com.google.android.material:material:1.9.0'

    //Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.mockito:mockito-core:1.10.19'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    //Lombok
    compileOnly 'org.projectlombok:lombok:1.18.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'

    //File Picker
    implementation 'com.github.angads25:filepicker:1.1.1'

    //Mac Text
    implementation 'com.github.phearme:macaddress-edittext:1.0.1'

    //Rest API
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.6.0'
    implementation 'commons-io:commons-io:2.6'

    //Color Picker
    implementation 'com.github.alexandre-g:AndroidPhotoshopColorPicker:1.2.4'

    //Device Name
    implementation 'com.jaredrummler:android-device-names:1.1.9'

}